# List comprehensions

## Basic concepts

On mathematics, the comprehension notation can be used to construct new sets from existing sets.

- For example, the comprehension $\{x^2 | x \in \{1..5\}\}$ produces the set $\{1, 4, 9, 16, 25\}$ of all numbers $x^2$ such that $x$ is an element of the set $\{1..5\}$.

In Haskell, a similar comprehension notation can be used to construct new lists from existing lists. For example:

```haskell
> [x^2 | x <- [1..5]]
[1,4,9,16,25]
```

- The symbol `|` is read as such that, `<-` is read as is drawn from, and the expression `x <- [1..5]` is called a generator.

- A list comprehension can have more than one generator, with successive generators being separated by commas.
- Note: different order of the two generators produces the same set of pairs, but arranged in a different order
  - These behaviors can be understood by thinking of **later generators as being more deeply nested**

```haskell
> [(x, y) | x <- [1..3], y <- [6,7]]
[(1,6),(1,7),(2,6),(2,7),(3,6),(3,7)]

-- later generator is more deeply nested
-- for x in [1..3]:
--     for y in [6, 7]:
--         ...
---------------------------------------------
> [(x, y) | y <- [6,7], x <- [1..3]]
[(1,6),(2,6),(3,6),(1,7),(2,7),(3,7)]
-- later generator is more deeply nested
-- for y in [6, 7]:
--     for x in [1..3]:
--         ...
```

- Later generators can also depend upon the values of variables from earlier generators.

```haskell
> [(x, y) | x <- [1..3], y <- [x+10, x+20]]
[(1,11),(1,21),(2,12),(2,22),(3,13),(3,23)]
```

- Example: the library function `concat` that concatenates a list of lists can be defined by using one generator to select each list in turn, and another to select each element from each list

```haskell
concat' :: [[a]] -> [a]
concat' xss = [x | xs <- xss, x <- xs]
-- same as xss being list of list, so xs is each list, and x is element in list
-- for xs : xss:
--    for x : xs:
--        res += [x]
```

- The wildcard pattern `_` is sometimes useful in generators to discard certain elements from a list.

```haskell
firsts :: [(a,b)] -> [a]
firsts ps = [x | (x,_) <- ps]

> firsts [(1,2),(3,4)]
[1,3]
```

- The library function that calculates the length of a list can be defined by replacing each element by one and summing the resulting list

```haskell
length' :: [a] -> Int
length' xs = sum [1 | _ <- xs]

> length' [1,2,3,4]
4
```

## Guards

- List comprehensions can also use logical expressions called **guards** to filter the values produced by earlier generators.

```haskell
> [x | x <- [1..10], even x]
[2,4,6,8,10]

factors :: Int -> [Int]
factors n = [x | x <- [1..n], n `mod` x == 0]

> factors 15
[1,3,5,15]
> factors 23
[1,23]

prime :: Int -> Bool
prime n = factors n == [1,n]

> prime 15
False
> prime 23
True

-- Note that deciding that a number such as 15 is not prime does not require the
-- function `prime` to produce all of its factors, because under lazy evaluation
-- the result False is returned as soon as any factor other than one or the
-- number itself is produced.

primes :: Int -> [Int]
primes n = [x | x <- [2..n], prime x]
> primes 40
[2,3,5,7,11,13,17,19,23,29,31,37]
```

```haskell
-- get_val :: Eq key => key -> [(key, val)] -> [val]
-- e.g. for any key that support Eq ... give list of key_val_pair, return vals
get_val k key_val_pair = [v | (k', v) <- key_val_pair, k == k']
> get_val 'b' [('a', 1),('b', 2),('b', 3)]
[2,3]
```

## The `zip` function

- The library function `zipp` produces a new list by pairing successive elements from two existing lists **until either or both** lists are exhausted.

```haskell
> zip ['a', 'b', 'c'] [1, 2, 3]
[('a',1),('b',2),('c',3)]

-- until either or both** lists are exhausted.
> zip ['A', 'B', 'C'] [1, 2]
[('A',1),('B',2)]
```

- The function `zip` is often useful when programming with list comprehensions

```haskell
pairs :: [a] -> [(a, a)]
pairs xs = zip xs (tail xs)

> pairs [1,2,3,4]
[(1,2),(2,3),(3,4)]
```

- Then using `pairs` we can now define a function that decides if a list of elements of any ordered type is sorted by simply checking that all pairs of adjacent elements from the list are in the correct order:

```haskell
sorted :: Ord a => [a] -> Bool
sorted xs = and [x <= y | (x, y) <- pairs xs]

> sorted [1,2,3,4]
True
> sorted [1,3,2,4]
False
```

- Using `zip` we can also define a function that returns the list of all positions at which a value occurs in a list
  - Note: the expression `[0 ..]` produces the list of indices `[0,1,2,3,...]`.
  - This list is notionally infinite, but **under lazy evaluation only as many elements of the list as required** by the context in which it is used, in this case zipping with the input list x s, will actually be produced.
  - Exploiting lazy evaluation in this manner avoids the need to explicitly produce a list of indices of the same length as the input list.

```haskell
positions :: Eq a => a -> [a]
positions x xs = [i | (x', i) <- zip xs [0..], x == x']

> positions False [True, False, True, False]
[1,3]
```

## String comprehensions

- String are not primitive in Haskell, they are constructed as lists of characters. 
  - The string `"abc" :: String` is ust an abbreviation for the list of characters `['a', 'b', 'c'] :: [Char]`
- Because strings are lists, any polymorphic function on lists can also be used with strings.

```haskell
> "abcde" !! 2
'c'
```

- For the same reason, list comprehensions can also be used to define functions on strings

```haskell
-- number of elements that is lower case
lowers :: String -> Int
lowers xs = length [x | x <- xs, x >= 'a' && x < 'z']

-- count the occurrence of x in xs
count :: Char -> String -> Int
count x xs = length [x' | x' <- xs, x == x']
```

## Example: The Caesar cipher

- To encode a string, Caesar simply replaced each letter in the string by the letter three places further down in the alphabet, wrapping around at the end of the alphabet
- The specific shift factor of three used by Caesar can be replaced by any integer between one and twenty-five, thereby giving twenty-five different ways of encoding a string.
- For simplicity, we will only encode the lower-case letters within a string

```haskell
import Data.Char -- ord etc comes from this lib

lower_2_Int c = ord c - ord 'a'
int_2_lower n = chr(ord 'a' + n)
shift n c | isLower c = int_2_lower((lower_2_Int c + n) `mod` 26)
          | otherwise = c
encode n xs = [shift n x | x <- xs]
```

- The key to cracking the Caesar cipher is the observation that some letters are used more frequently than others in English text. Say it's:

```haskell
table=[8.1,1.5,2.8,4.2,12.7,2.2,2.0,6.1,7.0,
       0.2,0.8,4.0,2.4,6.7,7.5,1.9,0.1,6.0,
       6.3,9.0,2.8,1.0,2.4,0.2,2.0,0.1]
```

- Suppose that we are given an encoded string, but not the shift factor
that was used to encode it, and wish to determine this number in order that we can decode the string.
  - This can usually be achieved by producing the frequency table of the encoded string
  - calculating the chi-square statistic for each possible rotation of this table with respect to the table of expected frequencies, and
  - using the position of the minimum chi-square value as the shift factor.
    - A standard method for comparing a list of observed frequencies `os` with a list of expected frequencies `es` is the chi-square

```haskell
-- fromIntegral: convert int to float
percent n m = (fromIntegral n / fromIntegral m) * 100
-- percent 5 15 get a 33.333336

freqs xs = [percent (count x xs) (length xs) | x <- ['a' .. 'z']]
-- freqs gives the probability distribution of each char in the input xs

-- according to the math formula
chisqr os es = sum[((o - e)^2)/e | (o, e) <- zip os es]

rotate n xs = drop n xs ++ take n xs
> rotate 3 [1,2,3,4,5]
[4,5,1,2,3]
```

```haskell
table' = freqs "kdvnhoo lvixq"
[chisqr (rotate n table') table | n = [0..25]]
```

- the minimum value of result should indicate the shifting factor, which we can use this to extract:

```haskell
-- getting the position of a value x in xs
positions x xs = [i | (x', i) <- zip xs [0..], x == x']
```

- So overall:

```haskell
crack xs = encode (-factor) xs
  where
    factor = head(positions (minimum chitab) chitab)
    chitab = [chisqr (rotate n table') table | n <- [0..25]]
    table' = freqs xs
```

- Note, however, that it may not be successful if the string is short or has an unusual distribution of letters.
- Check all the code in [here](../src/ch5.caesar_cipher.hs)

