# Higher-order function

## Basic concepts

- Functions with multiple arguments are usually defined in Haskell using the notion of currying.
- That is, the arguments are taken one at a time by exploiting the fact that functions can return functions as results.

```hs
add :: Int -> Int -> Int
add x y = x + y
-- means
add :: Int -> (Int -> Int)
add = \x -> (\y -> x + y)
```

- In Haskell, it is also permissible to define functions that take functions as arguments

```hs
twice :: (a -> a) -> a -> a
twice f x = f (f x)

> twice (*2) 3
12
> twice reverse [1,2,3] -- apply reverse twice == no reverse
[1,2,3]
```

- Formally speaking, a function that **takes a function as an argument** or **returns a function as a result** is called a **higher-order function**.
  - In practice, however, because the term curried already exists for returning functions as results, the term **higher-order** is often just used for **taking functions as arguments**.

## Processing lists

- The standard prelude defines a number of useful higher-order functions for processing lists.

Example 1: `map`

```hs
map :: (a -> b) -> [a] -> [b]
map f xs = [f x | x <- xs]

> map (+1) [1,3,5,7]
[2,4,6,8]
```

- it is a polymorphic function that can be **applied to lists of any type, as are most higher-order functions on lists.**
- it can be applied to itself to process nested lists.

```txt
map (map (+1)) [[1,2,3],[4,5]]
= {applying outer map} [map (+1) [1,2,3], map (+1) [4,5]]
= {applying inner map} [[2,3,4][5,6]]
```

Example 2: `filter`

```hs
filter :: (a -> Bool) -> [a] -> [a]
filter p xs = [x | x <- xs, p x]

> filter (/= ' ') "abc def ghi"
"abcdefghi"
```

Use together to calculate the sum of the squares of the even integers from a list

```hs
sumsqreven ns = sum (map (^2) (filter even ns))
```

Other frequently used Prelude higher-order functions

```hs
-- Decide if all elements of a list satisfy a predicate:
> all even [2,4,6,8]
True

-- Decide if any element of a list satisfies a predicate:
> any odd [2,4,6,8]
False

-- Select elements from a list while they satisfy a predicate
> takeWhile even [2,4,6,7,8]
[2,4,6]

-- Remove elements from a list while they satisfy a predicate:
> dropWhile even [2,4,6,7,8]
[7,8]
```

## The `foldr` function

Many functions that take a list as their argument can be defined using the following simple pattern of recursion on lists

```hs
f[] = v
f (x:xs) = x # f xs
```

- Maps the empty list to a value `v`
- any non-empty list to an operator `#` applied to the head of the list and the result of recursively processing the tail.

- Examples:

```hs
sum [] = 0
sum (x:xs) = x + sum xs
```

```hs
product[] = 1
product (x:xs) = x * product xs
```

```hs
or [] = False
or (x:xs) = x || or xs
```

```hs
and [] = True
and (x:xs) = x && and xs
```

- The higher-order library function `foldr` (abbreviating **fold right**) encapsulates this pattern of recursion for defining functions on lists, with the operator `#` and the value `v` as arguments.
- For example, using `foldr` the four definitions above can be rewritten more compactly as follows:

```hs
sum = foldr (+) 0

product = foldr (*) 1

or = foldr (||) False

and = foldr (&&) True
```

- These new definitions could also include explicit list arguments (but we prefer the above definitions with implicit arguments instead as it's simpler)

```hs
sum xs = foldr (+) 0 xs

product xs = foldr (*) 1 xs

or xs = foldr (||) False xs

and xs = foldr (&&) True xs
```

- The `foldr` function itself can be defined using

```hs
foldr f v [] = v
foldr f v (x:xs) = f x (foldr f v xs)
```

- In practice, however, it is best to think of the behavior of `foldr f v` in a non-recursive manner, as simply replacing each cons operator in a list by the function `f`, and the empty list at the end by the value `v`:
  - `sum = foldr (+) 0` states that summing a list of numbers amounts to replacing each cons by addition and the empty list by zero.

```hs
foldr (+) 0 [1,2,3]
-- think of as cons operator in the list 1 : (2 : (3 : [])) then replace with f
= 1 + (2 + (3 + 0))
```

- `foldr`  can be used to define many more functions than might first be expected

- For example:

```hs
length [] = 0
length (_:xs) = 1 + length xs

-- it's like 1 : (2 : (3 : [])) being replaced with (1 + (1 + (1 + 0)))
-- hence it could have been defined as

length' = foldr (\_ n -> 1 + n) 0

{- the length of a list amounts to replacing each cons by the function that
adds one to its second argument, and the empty list by zero. Note that n is
like the recursion result of remaining element. Hence the function is like
"given n = recursion result of remaining list", we return 1 + n. -}
```

```hs
reverse [] = []
reverse (x:xs) = reverse xs ++ [x]

-- 1 : (2 : (3 : [])) => (([] ++ [3]) ++ [2]) ++ [1]
-- not easy to use foldr directly as there is no input list in the definition

-- workaround: define "cons"' reverse operator snoc as:

snoc x xs = xs ++ [x]

-- then we can define:

reverse [] = []
reverse (x:xs) = snoc x (reverse xs)

-- and then we can use foldr

reverse' = foldr snoc []
```

- Noting that the name fold right reflects the use of an operator that is assumed to **associate to the right**. Generally, the behavior of `foldr`can be summarized as follows:

```hs
foldr (#) v [x0,x1,...,xn] = x0 # (x1 # (...(xn # v)))
```

## The `foldl` function

- It is also possible to define recursive functions on lists using an operator that is assumed to associate to the left. For example, sum can be processed in this way:

```txt
sum [1,2,3]
= sum' 0 [1,2,3]
= sum' (0 + 1) [2,3]
= sum' ((0 + 1) + 2) [3]
= sum' (((0 + 1) + 2) + 3) []
= (((0 + 1) + 2) + 3)
```

- Generalizing from the `sum` example, many functions on lists can be defined using the following simple pattern of recursion:

```hs
f v [] = v
f v (x:xs) = f (v # x) xs
```

- The function maps the empty list to the accumulator value `v`, and
- any non-empty list to the **result of recursively processing the tail** using **a new accumulator value** obtained by **applying an operator `#` to the current value** and the head of the list.
- The higher-order library function `foldl` (abbreviating fold left) encapsulates this pattern of recursion, with the operator `#` and the accumulator `v` as arguments

```hs
sum = foldl (+) 0

product = foldl (*) 1

or = foldl (||) False

and = foldl (&&) True

length = foldl (\n _ -> n + 1) 0
-- length [1,2,3] = ((0+1)+1)+1 = 3

reverse = foldl (\xs x -> x:xs) []
-- reverse [1,2,3] = 3 : (2 : (1 : [])) = [3,2,1]
```

- When a function can be defined using both `foldr` and `foldl`, as in the above examples, the choice of which definition is preferable is usually made on grounds of **efficiency** and requires careful consideration of the evaluation mechanism underlying Haskell. (Discussed in Ch. 15)

- `foldl` can be defined using recursion:

```hs
foldl f v [] = v
foldl f v (x:xs) = foldl f (f v x) xs
```

- In practice, as with `foldr` it is best to think of the behavior of `foldl` in a non-recursive manner, in terms of an operator `#` that is assumed to associate  to the left, as summarized by the following equation:

```hs
foldl (#) v [x0,x1,...,xn] = (...((v # x0) # x1)...) # xn
```

## The composition operator

- The higher-order library operator `.` returns the composition of two functions as a single function

```hs
f . g = \x -> f (g x)
```

- `f . g`, which is read as `f` composed with `g`, is the function that takes an argument `x`, applies the function `g` to this argument, and applies the function `f` to the result.
- This operator could also be defined by `(f . g) x = f (g x)`. However, we prefer the above definition in which the `x` argument is shunted to the body of the definition using a lambda expression, because it makes explicit the idea that **composition returns a function as its result**.
- Composition can be used to simplify nested function applications, by reducing parentheses and avoiding the need to explicitly refer to the initial argument.

```hs
odd n = not (even n)
-- is equivalent to
odd = not . even

twice f x = f (f x)
-- is equivalent to
twice f = f . f

sumsqreven ns = sum (map (^2) (filter even ns))
-- is equivalent to
sumsqreven = sum . map (^2) . filter even
```

- The last definition exploits the fact that **composition is associative**. That is, `f . (g . h) = (f . g) . h` for any functions `f`, `g`, and `h` of the appropriate types
- Hence, in a composition of three of more functions, as in `sumsqreven`, there is no need to include parentheses to indicate the order of association, because associativity ensures that this does not affect the result.

- In addition to being associative, composition also has an identity, given by the identity function:

```hs
id = \x -> x
```

- `id` is the function that simply returns its argument unchanged, and has the property that `id . f = f` and `f . id = f` for any function `f`.
  - The identity function is often useful when reasoning about programs, and also provides a suitable starting point for a sequence of compositions.
  - For example, the composition of a list of functions can be defined as follows:

```hs
compose = foldr (.) id
```

## Example 1: Binary string transmitter

- To simplify the definition of certain functions, we assume for the remainder of this example that binary numbers are written in reverse order to normal, `1101` would now be written as `1011`, with successive bits as we move to the right increasing in weight by a factor of two: (E.g. `1011 = 1 * 1 + 0 * 2 + 1 * 4 + 1 * 8`)

- You can define

```hs
bin2int bits = sum [w * b | (w, b) <- zip weights bits]
  where
    weights = iterate (*2) 1
```

- where

```hs
iterate f x = [x, f x, f (f x), f (f (f x)), ...]
```

- But there is another simpler way using the fact that ...

```txt
(1 * a) + (2 * b) + (4 * c) + (8 * d)
= a + 2 * (b + 2 * c + 4 * d)
= a + 2 * (b + 2 * (c + 2 * d))
= a + 2 * (b + 2 * (c + 2 * (d + 2 * 0)))
```

- E.g. it's basically a `foldr` operation with initial value 0

```hs
bin2int = foldr (\x y -> x + 2 * y) 0
```

- How about int 2 bin?

```txt
13 / 2 = 6, remainder 1
 6 / 2 = 3, remainder 0
 3 / 2 = 1, remainder 1
 1 / 2 = 0, remainder 1
```

- So recursive way is like:


```hs
int2bin 0 = []
int2bin n = n % 2 : int2bin (n `div` 2)
```

- Then we can define encode method with:


```hs
-- padding to 8 bits
make8 bits = take 8 (bits ++ repeat 0)

-- for each char in input
--     convert char to int (ord)
--     then convert int to bits (int2bin)
--     then padding 8 bits (make8) as if it's unicode
-- Finally concat the whole result.
encode = concat . map (make8 . int2bin . ord)
```

- and decode method with

```hs
-- split each 8 bits in a list to form a list of list
chop8 [] = []
chop8 bits = take 8 bits : chop8 (drop 8 bits)


-- for each 8 bits chopping from input
-- apply bin2int to convert to int
-- then apply chr to convert int to c har
decode = map (chr . bin2int) . chop8
```

- finally, simulate a transmission:

```hs
-- transmit is like ...  encode the input, send through channel, then decode
transmit = decode . channel . encode
```

```bash
*Main> transmit "joeys"
"joeys"
```

## Example 2: First past the post voting algorithm

- Each person has one vote, and the candidate with the largest number of votes is declared the winner.

```hs
import Data.List  -- for sort

-- The type is like ... first a is arbitrary Eq'able value x
-- which is used for x to bind with filter, then feed the
-- list [a] to length. Finally, the output list return the Int
-- as the count result.

count :: Eq a => a -> [a] -> Int
-- counts the number of times that a given value occurs in a list
count x = length . filter (==x)


-- The first time you see a unique x, it's used as result
-- then it becomes what will be filtered out
rmdups :: Eq a => [a] -> [a]
--  removes duplicate values from a list
rmdups [] = []
rmdups (x:xs) = x : filter (/=x) (rmdups xs)

-- result of a first-past-the-post election in increasing order of the number
-- of votes received:

first_past_post vs = sort [(count v vs, v) | v <- rmdups(vs)]


-- taking last element from first_past_post, which is the pair
-- (max_vote_cnt, element_name), then take the second (snd) val
-- of the pair as result
winner :: Ord a => [a] -> a
winner = snd . last . first_past_post
```

## Example 3: Alternative voting algorithm

- Each person can vote for as many or as few candidates as they wish, listing them in preference order on their ballot
- To decide the winner, any empty ballots are first removed, then the candidate with the smallest number of 1st-choice votes is eliminated from the ballots, and same process is repeated until only one candidate remains, who is then declared the winner.

```txt
voter1: [r, g]
voter2: [b]
voter3: [g, r, b]
voter4: [b, g, r]
voter5: [g]

--> r only has 1 ticket for 1st place, removed r

voter1: [g]
voter2: [b]
voter3: [g, b]
voter4: [b, g]
voter5: [g]

--> g has 3 1st place, b has only 2. Remove b

voter1: [g]
voter2: []
voter3: [g]
voter4: [g]
voter5: [g]

--> g is the winner
```

## Questions from doing exercise ... (chatGPT answered)

Why `all p = and . map p`; not `all p = and . map . p`?


- The reason is that the type signature of `map` is `map :: (a -> b) -> [a] -> [b]`.
  - It takes a function that maps an element of type `a` to an element of type `b`, and applies that function to each element of a list of type `[a]`, returning a list of type `[b]`.
  - Therefore, when we use `map p`, we apply the function `p` to each element of the list, returning a list of booleans.

- The type signature of and is `and :: [Bool] -> Bool`.
  - It takes a list of booleans and returns True if all elements are True, and False otherwise.

- Therefore, `and . map p` first maps the function `p` over a list, producing a list of booleans, and then applies and to the resulting list of booleans.
- This is exactly what we want the all function to do: it takes a predicate `p` and a list, and returns True if `p` is true for all elements of the list, and False otherwise.

On the other hand, all p = and . map . p is not a valid definition of all. The expression map . p is not a function - it is a partial application of the map function to the function p. Therefore, applying map . p to a list would result in a type error.