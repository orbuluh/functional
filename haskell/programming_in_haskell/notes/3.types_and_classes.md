# Types and classes

## Basic concepts

- :man_teacher: A **type** is a collection of related values.
- We use the notation `v :: T` to mean that `v` is a value in the type `T`, and say that `v` has type `T`.
- The type `T - > T` contains all functions that map arguments from type T results from type T

```haskell
False :: Bool
True :: Bool
not :: Bool -> Bool
```

- More generally, the symbol `::` can also be used with expressions that have not yet been evaluated, in which case the notation `e :: T` means that evaluation of the expression `e` will produce a value of type `T`.

```haskell
not False :: Bool
not True :: Bool
```

- In Haskell, every expression must have a type, which is **calculated prior to evaluating the expression by a process** called **type inference**.
- The key to this process is the following simple typing rule for function application, which states that if `f` is a function that maps arguments of type `A` to results of type `B`, and `e` is an expression of type `A`, then the application `f e` has type `B`:

```haskell
f :: A -> B
e :: A
f e :: B
```

- For example, the expression `not 3` does not have a type under the above rule, because this would require that `3 :: Bool`, which is not valid because 3 is not a logical value.
- **Expressions that do not have a type** are said to contain a **type error**, and are deemed to be invalid expressions.

- **Because type inference precedes evaluation, Haskell programs are type safe, in the sense that type errors can never occur during evaluation**.
- The downside of type safety is that **some expressions that evaluate successfully will be rejected on type grounds**.
  - For example, the conditional expression `if True then 1 else False`  evaluates to the number 1, but contains a type error and is hence deemed invalid. (The typing rule for a conditional expression requires that both possible results have the same type, whereas in this case the first such result, `1`, is a number and the second, `False`, is a logical value.)
  - In GHCi, the type of any expression can be displayed by preceding the expression by the command `:type`

```haskell
> :type not
not :: Bool -> Bool
```

## Basic types

`Bool`: logical values.

`Char`: single characters

- This type contains all single characters in the Unicode system
- As in most programming languages, single characters must be enclosed in single forward quotes `'`.

`String`: strings of characters

- Strings of characters must be enclosed in double quotes `"`

`Int`: fixed-precision integers

- This type contains integers, with a fixed amount of memory being used for their storage. (range -2^63 to 2^63 - 1)

`Integer`: arbitrary-precision integers

- This type contains all integers, with as much memory as necessary being used for their storage, thus avoiding the imposition of lower and upper limits on the range of numbers
- Of type `Int` and `Integer`, the choice between these two types is also one of performance. In particular, most computers have built-in hardware for fixed-precision integers, whereas arbitrary-precision integers are usually processed using the slower medium of software, as sequences of digits.

`Float`: single-precision floating-point numbers

- This type contains numbers with a decimal point, with a fixed amount of memory being used for their storage.
- term floating-point comes from the fact that the number of digits permitted after the decimal point depends upon the size of the number
  - `sqrt 2 :: Float` using GHCi gives the result 1.4142135
  - `sqrt 9999 :: Float` gives 316.2262

`Double` – double-precision floating-point numbers

- similar to `Float`, except that twice as much memory is used for
storage of these numbers to increase their precision.
  - `sqrt 2 :: Double` using GHCi gives the result 1.4142135623730951


## List types

- We write `[T]` for the type of all lists whose elements have type `T`:

```haskell
[False, True, True] :: [Bool]
['a', 'b', 'c'] :: [Char]
[['a', 'b', 'c'], ['a', 'b']] :: [[Char]]
```

- There are no restrictions on the type of the elements of a list.
- There is no restriction that a list must have a finite length.
-  `[[]]` are called singleton lists, and is different from `[]`. The
former being a singleton list comprising the empty list as its only element, the latter being simply the empty list that has no elements.

## Tuple types

- `(T1, T2, ... Tn)`: A tuple is a finite sequence of components of **possibly different** types

```haskell
(False, 'a', True) :: (Bool, Char, Bool)
(['a', 'b', 'c'], [False, True]) :: ([Char], [Bool])
[('a', False), ('b', True)] :: [(Char, Bool)]
```

- The number of components in a tuple is called its **arity**.
  - The tuple `()` of arity  zero is called the empty tuple, tuples of arity two are called pairs, tuples of arity three are called triples, and so on.
- Tuples of arity one, such as `(False)`, are not permitted because **they would conflict with the use of parentheses to make the evaluation order explicit**. E.g. `(1+2)*3`
- tuples must have a finite arity, in order to ensure that tuple
types can always be inferred prior to evaluation.

## Function types

- A function is a mapping from arguments of one type to results of another type.
- The Haskell convention is **preceding function definitions by their types**, which serves as useful documentation.

```haskell
zeroto :: Int -> [Int]
zeroto n = [0..n]
```

- Any such types provided manually by the user are checked for consistency with the types calculated automatically using type inference.
- Note that there is no restriction that functions must be *total* on their argument type, in the sense that **there may be some arguments for which the result is not defined.**

```haskell
> head[]
*** Exception: Prelude.head: empty list
```

## Curried functions

- Functions are free to return functions as results. Functions with multiple arguments can also be handled in a way that takes their arguments **one at a time**. When they are done this way, they are called curried functions.

```haskell
add' :: Int -> (Int -> Int)
add' x y = x + y
```

- `add'` takes an integer `x` followed by an integer `y`, and returns the result `x + y`. More precisely, `add'` takes an integer `x` and returns a function, which in turn takes an integer `y` and returns the result `x + y`
- Comparison: `add` below produce the same result as `add'`, but `add` takes its two arguments at the same time packaged as a pair, `add'` takes its two arguments one at a time.

```haskell
add :: (Int, Int) -> Int
add (x, y) = x + y
```

- Functions with more than two arguments can also be handled using the same technique.

```haskell
mult :: Int -> (Int -> (Int -> Int))
mult x y z = x * y * z
```

- Curried functions are more flexible than functions on tuples, because useful functions can often be made by partially applying a curried function with less than its full complement of arguments. For example:

```haskell
> map (add' 1) [1..3]
[2,3,4]
```

- On the country, you can't do below.
  - Because `add (x, y) = x + y` takes a pair of input. Without defining it as if curried function, you can't wrap it with higher order function `map` and produce the result

```haskell
> map (add 1) [1..3]

<interactive>:16:1: error:
    • Non type-variable argument in the constraint: Num (a -> b)
      (Use FlexibleContexts to permit this)
    • When checking the inferred type
        it :: forall a b.
              (Num a, Num (a -> b), Num (a -> b, a -> b), Enum a) =>
              [b]
```

- In chapter 4 we will see how the meaning of curried function definitions can be formalized in a simple manner using the notion of lambda expressions. Simply put:

```haskell
add' x y = x + y
-- is the same as define with lambda
add'' x = \y -> x + y
-- the use of \ and -> implies a lambda function
-- overall it's like add'' taking one input x
-- and return a (nameless lambda) function that takes
-- one input y and return x + y as result.
-- And then you can do the same trick and say:
add''' = \x -> (\y -> x + y)
```

- (From [Computerphile video](https://youtu.be/psmu_VAuiag)) A real world example to understand curried function is like ... when you go to an ATM and withdraw money (say ATM always gives you 100 in return, if card and pin is correct), it could be like the ATM is asking for few parameters like

```haskell
atm_withdraw card pin request = 100
```

- but in reality, you are not putting all input at the same time to the atm, you insert the card, input the pin, then select the result. So it's actually more like:

```haskell
atm_withdraw = \card -> (\pin -> (\request = 100))
```

- Note: To avoid excess parentheses when working with curried functions, two simple conventions are adopted.
  - 1. the function arrow `->` in types is assumed to associate to the right. E.g. `Int -> Int -> Int -> Int` implies `Int -> (Int -> (Int -> Int))`
  - 2. function application, which is denoted silently using spacing, is assumed to associate to the left. E.g. `mult x y z` means `((mult x) y) z`
- Unless tupling is explicitly required, **all functions in Haskell with multiple arguments are normally defined as curried functions**, and the two conventions above are used to reduce the number of parentheses that are required.

## Polymorphic types

```haskell
> length [1, 3, 5, 7]
4
> length ["Yes", "No"]
2
> length [sin, cos, tan]
3
```

- The idea that `length` can be applied to lists whose elements have any type is made precise in its type by the inclusion of a **type variable.**
- Type variables must begin with a lower-case letter, and are usually simply named a, b, c, and so on. For example, the type of `length` is as follows:

```haskell
length :: [a] -> Int
```

- A type that contains one or more type variables is called **polymorphic** as is an expression with such a type.
  - Hence, `[a] -> Int` is a polymorphic type and `length` is a polymorphic function

## Overloaded types

```haskell
> 1 + 2
3
> 1.0 + 2.0
3.0
```

- The idea that `+` can be applied to numbers of any numeric type is made precise in its type by the inclusion of a **class constraint**. 
- Class constraints are written in the form `C a`, where `C` is the name of a class and `a` is a type variable.

- For example, the type of the addition operator `+` is as follows:

```haskell
(+) :: Num a => a -> a -> a
```

- For any type `a` that is an instance of the class `Num` of numeric types, the function `(+)` has type `a -> a -> a`. (Parenthesising an operator converts it into a curried function, as we shall see in chapter 4.)
- A type that contains one or more class constraints is called overloaded, as is an expression with such a type.
- Numbers themselves are also overloaded.
  - For example, `3 :: Num a => a` means that for any numeric type a, the value 3 has type a.
  - In this manner, the value 3 could be an integer, a floating-point number, or more generally a value of any numeric type, depending on the context in which it is used.

## Basic classes

A **type** is a collection of related **values**. A **class** is a collection of **types** that support certain overloaded operations called **methods**.

- Haskell provides a number of basic classes that are built-in to the
language, of which the most commonly used are described below.

:ice_cube: `Eq`: equality types

- This class contains types whose values can be compared for equality and inequality using the following two methods:

```haskell
(==) :: a -> a -> Bool
(/=) :: a -> a -> Bool
```

:ice_cube: `Ord`: ordered types

- This class contains types that are instances of the equality class `Eq`, but in addition whose values are totally (linearly) ordered, and as such can be compared and processed using the following six methods:

```haskell
(<) :: a -> a -> Bool
(<=) :: a -> a -> Bool
(>) :: a -> a -> Bool
(>=) :: a -> a -> Bool
min :: a -> a -> a
max :: a -> a -> a
```

:ice_cube: `Show`: showable types

- This class contains types whose values can be converted into strings of characters with method:

```haskell
show :: a -> String
```

:ice_cube: `Read`: readable types

This class is dual to S h o w, and contains types whose values can be converted from strings of characters with method:

```haskell
read :: String -> a
```

```haskell
> read "('a', False)" :: (Char, Bool)
('a', False)
```

- The use of `::` in these examples resolves the type of the result, which would otherwise not be able to be inferred by GHCi. In practice, however, the necessary type information can usually be inferred automatically from the context.
- Note that the result of `read` is undefined if its argument is not syntactically valid.

```haskell
> not (read "abc")
*** Exception: Prelude.read: no parse
```

- `(read "abc")` will get a String, and is not a logical value. So can't be `not()`

:ice_cube: `Num`: numeric types

- This class contains types whose values are numeric, and as such can be processed using the following six methods:

```haskell
(+) :: a -> a -> a
(-) :: a -> a -> a
(*) :: a -> a -> a
negate :: a -> a
abs :: a -> a
signum :: a -> a -- (signum returns the sign)
```

- Note that negative numbers must be parenthesized when used as
arguments to functions, to ensure the correct interpretation of the minus sign.

```haskell
> signum (-3)
-1
> abs (-3)
3
```

- `abs -3` means `abs - 3`, which is both the incorrect meaning here and an ill-typed expression
- Note that the `Num` class does not provide a division method, but as we shall now see, division is handled separately using two special classes, one for integral numbers and one for fractional numbers.

:ice_cube: `Integral`: integral types

- This class contains types that are instances of the numeric class `Num`, but in addition whose values are integers, and as such support the methods of integer division and integer remainder:

```haskell
div :: a -> a -> a
mod :: a -> a -> a
```

- In practice, these two methods are often written between their two arguments by enclosing their names in single back quotes

```haskell
> 7 `div` 2
3
> 7 `mod` 2
1
```

- For efficiency reasons, a number of prelude functions that involve both lists and integers (such as `take` and `drop`) are restricted to the type `Int`of finite precision integers, rather than being applicable to any instance of the `Integral` class.
- If required, however, such generic versions of these functions are provided as part of an additional library file called `Data.List`

:ice_cube: `Fractional`: fractional types

- This class contains types that are instances of the numeric class `Num`, but in addition whose values are non-integral, and as such support the methods of fractional division and fractional reciprocation

```haskell
(/) :: a -> a -> a
recip :: a -> a
```

```haskell
> 7.0 / 2.0
3.5
> recip 2.0  # e.g. 1.0 / 2.0
0.5
```
